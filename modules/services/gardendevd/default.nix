{
  config,
  pkgs,
  lib,
  ...
}:
let
  cfg = config.services.gardendevd;

  package = pkgs.gardendevd.overrideAttrs (old: {
    version = "0.2-unstable-2026-07-03";

    src = old.src.override {
      tag = null;
      rev = "ec73dc569382404bc6620c9857b7e09206bc282e";
      hash = "sha256-8VOJFz5QtlyLbAf87rtNXSvnrfPoyQVAKwuD+YkfzdQ=";
    };

    mesonFlags = [
      (lib.mesonEnable "dracut" false)
      (lib.mesonEnable "uaccess" true)
    ];
  });

in
{
  options.services.gardendevd = {
    enable = lib.mkOption {
      type = lib.types.bool;
      default = false;
      description = ''
        Whether to enable [gardendevd](${cfg.package.meta.homepage}) as a system service.
      '';
    };

    package = lib.mkOption {
      type = lib.types.package;
      default = package;
      defaultText = lib.literalExpression "pkgs.gardendevd";
      description = ''
        The package to use for `gardendevd`.
      '';
    };

    debug = lib.mkOption {
      type = lib.types.bool;
      default = false;
      description = ''
        Whether to enable debug logging.
      '';
    };

    extraArgs = lib.mkOption {
      type = with lib.types; listOf str;
      default = [ ];
      description = ''
        Additional arguments to pass to `gardendevd`.
      '';
    };

    path = lib.mkOption {
      type = with lib.types; listOf path;
      default = [ ];
      description = ''
        Packages added to the {env}`PATH` environment variable when
        executing programs from udev rules.

        coreutils, gnu{sed,grep}, util-linux and kmod are
        automatically included.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    environment.systemPackages = [ cfg.package ];

    services.gardendevd.extraArgs = [
      "-K"
      "-v"
      (if cfg.debug then "debug" else "info")
    ];

    services.gardendevd.path = [
      config.programs.coreutils.package
      pkgs.gnugrep
      pkgs.gnused
      pkgs.kmod
      pkgs.util-linux
    ];

    # contribute gardendevd's bundled rules to the udev packages list
    services.udev.packages = [ cfg.package ];

    # gardendevd can read standard hwdb.bin under /etc/udev/hwdb.bin
    environment.etc."udev/hwdb.bin".source =
      pkgs.runCommand "gardendevd-hwdb.bin"
        {
          __structuredAttrs = true;
          preferLocalBuild = true;
          allowSubstitutes = false;
          packages = lib.unique config.services.udev.packages;
        }
        ''
          shopt -s nullglob

          mkdir -p root/etc/udev/hwdb.d
          for i in "''${packages[@]}"; do
            for j in "$i"/{etc,lib,var/lib}/udev/hwdb.d/*; do
              ln -s "$j" "root/etc/udev/hwdb.d/$(basename "$j")"
            done
          done

          ${package}/bin/gardendev-hwdb update --root "$PWD/root"
          mv root/etc/udev/hwdb.bin "$out"
        '';

    environment.etc."udev/rules.d".source =
      pkgs.runCommand "gardendevd-rules"
        {
          __structuredAttrs = true;
          preferLocalBuild = true;
          allowSubstitutes = false;
          packages = lib.unique config.services.udev.packages;
        }
        ''
          mkdir -p "$out"
          shopt -s nullglob

          for i in "''${packages[@]}"; do
            for j in "$i"/{etc,lib,var/lib}/udev/rules.d/*; do
              cat "$j" > "$out/$(basename "$j")"
            done
          done

          # gardendevd's own rules are authoritative on collision.
          for j in ${cfg.package}/lib/udev/rules.d/*; do
            cat "$j" > "$out/$(basename "$j")"
          done

          for i in "$out"/*.rules; do
            substituteInPlace "$i" \
              --replace-quiet \"/sbin/modprobe \"${lib.getExe' pkgs.kmod "modprobe"} \
              --replace-quiet \"/sbin/mdadm \"${pkgs.mdadm}/sbin/mdadm \
              --replace-quiet \"/sbin/blkid \"${pkgs.util-linux}/sbin/blkid \
              --replace-quiet \"/bin/mount \"${pkgs.util-linux}/bin/mount \
              --replace-quiet /usr/bin/readlink ${lib.getExe' config.programs.coreutils.package "readlink"} \
              --replace-quiet /usr/bin/cat ${lib.getExe' config.programs.coreutils.package "cat"} \
              --replace-quiet /usr/bin/basename ${lib.getExe' config.programs.coreutils.package "basename"} 2>/dev/null
          done
        '';

    providers.services.units.gardendevd = {
      description = "device event daemon (gardendevd)";

      # the head tier, beside the other device managers, so `sysinit` waits for device events
      requires = [ (lib.head config.providers.services.trunk.levels) ];

      type.service = {
        # the PATH goes in a wrapper rather than through the contract's `path`: dinit cannot
        # give a unit one, and the rules gardendevd runs invoke helpers by name.
        #
        # `"$@"` because the implementation appends the readiness descriptor to the command it
        # is given, and the command it is given is this wrapper - so the wrapper has to pass it
        # on rather than swallow it.
        command = pkgs.writeShellScript "gardendevd" ''
          export PATH=${lib.makeBinPath cfg.path}:$PATH
          exec ${cfg.package}/bin/gardendevd ${lib.escapeShellArgs cfg.extraArgs} "$@"
        '';

        # what the daemon can do, best first. The contract takes the best of these the
        # implementation can observe; `fork` last is what makes that always resolvable.
        #
        # `gardendevd --help`: `-D <fd>  Readiness notification file descriptor`. Which
        # descriptor is the implementation's business, and it appends it.
        readiness = [
          { s6.flag = "-D"; }
          "fork"
        ];
      };
    };

    # the two `run` stanzas become one oneshot. They were ordered against each other by finit
    # priority, which no other implementation has - and the ordering is the whole point, since
    # settling before the trigger settles nothing. In one script it is the shell's guarantee,
    # the same conclusion the shutdown side reached.
    #
    # Named `gardendevd-settle`, beside udev's `udev-settle` and mdevd's `coldplug`: attached
    # to the head tier, so `sysinit` waits for the device nodes to be there.
    providers.services.units.gardendevd-settle = {
      description = "trigger device events and wait for gardendevd to settle";

      requires = [
        (lib.head config.providers.services.trunk.levels)
        "gardendevd"
      ];

      type.oneshot.command = pkgs.writeShellScript "gardendevd-settle" ''
        ${cfg.package}/bin/gardendevctl trigger -c add -t all
        ${cfg.package}/bin/gardendevctl settle -t 30
      '';
    };

    # TODO: share between device managers
    system.activation.scripts.gardendevd = lib.mkIf config.boot.kernel.enable {
      text = ''
        # The deprecated hotplug uevent helper is not used anymore
        if [ -e /proc/sys/kernel/hotplug ]; then
          echo "" > /proc/sys/kernel/hotplug
        fi

        # Allow the kernel to find our firmware.
        if [ -e /sys/module/firmware_class/parameters/path ]; then
          echo -n "${config.hardware.firmware}/lib/firmware" > /sys/module/firmware_class/parameters/path
        fi
      '';
    };

    # build out the default initramfs image
    boot.initrd = {
      path = [
        config.services.gardendevd.package
      ];

      finit.services.gardendevd = {
        command = "gardendevd -K -D %n";
        notify = "s6";
      };

      finit.run = {
        "gardendevctl@1" = {
          command = "gardendevctl trigger -c add -t all";
          conditions = "service/gardendevd/ready";
          priority = 250;
        };
        "gardendevctl@2" = {
          command = "gardendevctl settle -t 30";
          conditions = "service/gardendevd/ready";
          priority = 260;
        };
      };

      # minimal set of rules needed for initramfs
      contents =
        map
          (v: {
            target = "/etc/udev/rules.d/${v}.rules";
            source = "${cfg.package}/lib/udev/rules.d/${v}.rules";
          })
          [
            "60-block"
            "60-persistent-storage"
            "80-drivers"
          ];
    };
  };
}
