{
  config,
  pkgs,
  lib,
  ...
}:
let
  pamOpts =
    { name, ... }:
    {
      options = {
        enable = lib.mkOption {
          type = lib.types.bool;
          default = true;
        };

        name = lib.mkOption {
          type = lib.types.str;
          default = name;
        };

        text = lib.mkOption {
          type = lib.types.lines;
        };
      };
    };

  cfg = config.security.pam;
in
{
  options.security.pam = {
    enable = lib.mkOption {
      type = lib.types.bool;
      default = true;
    };

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.pam;
    };

    debug = lib.mkOption {
      type = lib.types.bool;
      default = false;
    };

    services = lib.mkOption {
      type = with lib.types; attrsOf (submodule pamOpts);
      default = { };
    };

    environment = lib.mkOption {
      type = lib.types.attrsOf (
        lib.types.submodule {
          options =
            let
              optionType =
                with lib.types;
                let
                  atom = oneOf [
                    int
                    str
                    path
                  ];
                in
                nullOr (coercedTo atom lib.singleton (listOf atom));
            in
            {
              default = lib.mkOption {
                type = optionType;
                default = null;
                apply =
                  let
                    toStr = v: if lib.isPath v then "${v}" else toString v;
                  in
                  v: if v == null then null else lib.concatMapStringsSep ":" toStr v;
                description = ''
                  The `DEFAULT` environment variables to be set, unset or modified by {manpage}`pam_env(8)`. See
                  {manpage}`pam_env.conf(5)` for additional details.
                '';
              };

              override = lib.mkOption {
                type = optionType;
                default = null;
                apply =
                  let
                    toStr = v: if lib.isPath v then "${v}" else toString v;
                  in
                  v: if v == null then null else lib.concatMapStringsSep ":" toStr v;
                description = ''
                  The environment variables to be set, unset or modified by {manpage}`pam_env(8)`. See
                  {manpage}`pam_env.conf(5)` for additional details.
                '';
              };
            };
        }
      );
      default = { };
      description = "Set of rules for {manpage}`pam_env(8)`.";
    };
  };

  config = {
    environment.etc =
      let
        etcTree = lib.mapAttrs' (
          k: v:
          lib.nameValuePair "pam.d/${k}" {
            inherit (v) text;
          }
        ) (lib.filterAttrs (_: v: v.enable) config.security.pam.services);

        debug = lib.mkIf config.security.pam.debug {
          "pam_debug".text = "";
        };

        pam_env."security/pam_env.conf".text =
          let
            toField = key: val: lib.optionalString (val != null) " ${key}=${val}";
          in
          lib.concatMapAttrsStringSep "\n" (
            var: { default, override }: "${var}${toField "DEFAULT" default}${toField "OVERRIDE" override}"
          ) cfg.environment;
      in
      lib.mkMerge [
        debug
        pam_env
        etcTree
      ];

    security.pam.environment = lib.mapAttrs (_: default: { inherit default; }) {
      NIX_REMOTE = "daemon";
      NIX_XDG_DESKTOP_PORTAL_DIR = "/run/current-system/sw/share/xdg-desktop-portal/portals";
      PATH = [
        # first, and before the system profile: the setuid copies live here, and the ones in
        # the profile are not setuid. Found the wrong way round, sudo refuses with "must be
        # owned by uid 0 and have the setuid bit set" and su with "must be setuid root" -
        # which reads as the program being broken rather than as the wrong one being run.
        "/run/wrappers/bin"

        "/etc/profiles/per-user/@{PAM_USER}/bin"
        "/run/current-system/sw/bin"
      ];
      XCURSOR_PATH = [
        "/run/current-system/sw/share/icons"
        "/run/current-system/sw/share/pixmaps"
      ];
      XDG_CONFIG_DIRS = [
        "/etc/xdg"
        "/run/current-system/sw/etc/xdg"
      ];
      XDG_DATA_DIRS = [
        "/run/current-system/sw/share"
        "/etc/profiles/per-user/@{PAM_USER}/share"
      ];
    };

    security.pam.services.other = {
      text = ''
        auth     required pam_warn.so
        auth     required pam_deny.so
        account  required pam_warn.so
        account  required pam_deny.so
        password required pam_warn.so
        password required pam_deny.so
        session  required pam_warn.so
        session  required pam_deny.so
      '';
    };

    security.wrappers = {
      unix_chkpwd = {
        setuid = true;
        owner = "root";
        group = "root";
        source = "${cfg.package}/bin/unix_chkpwd";
      };
    };
  };
}
